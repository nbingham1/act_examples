import "globals.act";
import "channel.act";

namespace wchb
{

export defproc buffer(globals g; e1of<2> L, R)
{
	c1of<2> _R;
	prs<g.Vdd, g.GND>
	{
		g._sReset & R.e & L.d[0] -> _R.d[0]-
		g._sReset & R.e & L.d[1] -> _R.d[1]-
		~_R.d[0] -> R.d[0]+
		~_R.d[1] -> R.d[1]+

		R.d[0] | R.d[1] -> L.e-

		~g._pReset | ~R.e & ~L.d[0] -> _R.d[0]+
		~g._pReset | ~R.e & ~L.d[1] -> _R.d[1]+
		_R.d[0] -> R.d[0]-
		_R.d[1] -> R.d[1]-

		~R.d[0] & ~R.d[1] -> L.e+
	}
}

}

namespace pchb
{

export defproc buffer(globals g; e1of<2> L, R)
{
	c1of<2> _R;
	bool Rv;
	prs<g.Vdd, g.GND>
	{
		g._sReset & R.e & L.e & L.d[0] -> _R.d[0]-
		g._sReset & R.e & L.e & L.d[1] -> _R.d[1]-
		~_R.d[0] -> R.d[0]+
		~_R.d[1] -> R.d[1]+
		~_R.d[0] | ~_R.d[1] -> Rv+
		
		g._sReset & Rv -> L.e-
		
		~g._pReset | ~R.e & ~L.e -> _R.d[0]+
		~g._pReset | ~R.e & ~L.e -> _R.d[1]+
		_R.d[0] -> R.d[0]-
		_R.d[1] -> R.d[1]-
		_R.d[0] & _R.d[1] -> Rv-

		~g._pReset | ~Rv & ~L.d[0] & ~L.d[1] -> L.e+
	}
}

}

namespace pcfb
{

export defproc buffer(globals g; e1of<2> L, R)
{
	c1of<2> _R;
	bool en, _Le, Rv;
	prs<g.Vdd, g.GND>
	{
		en & R.e & L.d[0] -> _R.d[0]-
		en & R.e & L.d[1] -> _R.d[1]-
		~_R.d[0] -> R.d[0]+
		~_R.d[1] -> R.d[1]+
		~_R.d[0] | ~_R.d[1] -> Rv+
		en & Rv -> L.e-
		~L.e -> _Le+

		g.pReset | _Le -> en-

		~g._pReset | ~en & ~R.e -> _R.d[0]+
		~g._pReset | ~en & ~R.e -> _R.d[1]+
		~g._pReset | ~en & ~L.d[0] & ~L.d[1] -> L.e+
		_R.d[0] -> R.d[0]-
		_R.d[1] -> R.d[1]-
		_R.d[0] & _R.d[1] -> Rv-
		L.e -> _Le-

		~g.sReset & ~Rv & ~_Le -> en+
	}
}

}

